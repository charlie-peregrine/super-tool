/*------------------------------------------*/ 
/*--- MHC Governor Model for steam units ---*/
/*------------------------------------------*/ 

/*------------------------------------------------------- 
   input data:
      mailbox[0].number[1] : Set to '1' to sync
                             This function will reset it
      mailbox[0].number[2] : Initial Speed 
 
   input from generator:
   
   variables:
      epcmod[@mx].v3  = @g1 index for hp unit, set in INIT 

    states:

    output to generators:

    modes used for user written models are as follows:
       mode = 2;  MODEL_INIT 2
       mode = 3;  MODEL_SORC 3
       mode = 4;  MODEL_ALGE 4
       mode = 5;  MODEL_RATE 5
       mode = 7;  MODEL_OUTP 7
------------------------------------------------------- */



   /* !!!  DO NOT CHANGE THESE DEFINE STATEMENTS !!! */
   define INIT  2
   define SORC  3
   define ALGE  4
   define RATE  5
   define OUTP  7

   @mx = dypar[0].cmi  /* current model index */
   @k  = model[@mx].k  /* record index        */
   @mode = dypar[0].mode

   switch (@mode)

      /*---------------------------------------*/
      /*--- INIT - initialization of states ---*/
      /*---------------------------------------*/
      case INIT:

         /* determine indices */
         @ibs = model[@mx].bus
         $id1 = model[@mx].id
         @m1 = model_index(0, "sub.GenModelName", @ibs, -1, $id1, 0, -1)
 
         if (@m1<0)
            logterm(" missing generator model at ", busident(@ibs)," ",$id1, @m1)
            epcmod[@mx].v3 = @m1
         else
            @g1 = model[@m1].k
            epcmod[@mx].v3 = @g1
            epcmod[@mx].s0 = 1.0
            epcmod[@mx].zs0 = 1.0
            epcmod[@mx].v2 = 0.0
         endif   

         /* 12-2-2011 - moved these lines from ALGE case so you can modify PREF in main epcl */
         @g1 = epcmod[@mx].v3
         genbc[@g1].pref = epcmod[@mx].s0

      break


      /*--------------------------------------------*/
      /*-- ALGE - calculate algebraic quantities ---*/
      /*--------------------------------------------*/
      case ALGE:

         /* model index */
         @time = dypar[0].time
   
         if (mailbox[0].number[1] = 1.0)
   
            epcmod[@mx].v2 = 1.0
     
            @extflag = 1
            @frbus = 1
            @tobus = 0
            $id2 = "1"
            @sec = 1
            $modname = "sub.GenModelName"
            
            $parname = "speed"
            @value = mailbox[0].number[2]
            @ret = setmodpar ( @extflag, @frbus, @tobus, $id2, @sec, $modname, $parname, @value)
            if (@ret = -1)
               logterm(" speed was not set")
            endif
            
            $parname ="zspeed"
            @ret = setmodpar ( @extflag, @frbus, @tobus, $id2, @sec, $modname, $parname, @value)
            if (@ret = -1)
               logterm(" zspeed was not set")
            endif
            
            mailbox[0].number[1] = 0.0     
         endif     

      break


      /*-----------------------------------*/
      /*-- RATE - calculate derivatives ---*/
      /*-----------------------------------*/
      case RATE:

         if (epcmod[@mx].v2 < 0.9)
            epcmod[@mx].ds0 = 0.0
         else
            epcmod[@mx].ds0 = 0.00083 / 1.0
            if (epcmod[@mx].s0 >= 1.00083)
               epcmod[@mx].s0 = 1.00083
               epcmod[@mx].zs0 = 1.00083
               epcmod[@mx].ds0 = 0.0
            endif  
         endif
    
      break


      case SORC:
      break


      /*-------------------------------------*/
      /*-- OUTP - update output variables ---*/
      /*-------------------------------------*/
      case OUTP:
      break

   endcase

end   

